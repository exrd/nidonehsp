//=============================================================================
// guiTestFrame.nhsp

#module "guiTestFrame"

#deffunc global guiTestFrameInit
	// グローバル初期化
	if (!gIsInitialized) {
		gIsInitialized = 1
		gIsFontItalic = 0 : gIsFontShadow = 0 : gIsFontOutline = 0 : gIsFontHeight = 32.0
	}
	// ローカル初期化
	toReturn = 0
	buttonPushed = 0
	checked = 0
	masterFrame = 0
	// GUI初期化
	gsel 0 : cls
	objsize 200, 20 : pos 20, 120 : button gosub "ボタンをおしてください", *implPushButton
	objsize 200, 20 : pos 20, 150 : chkbox "チェックボックスのテスト", checked
	objsize 200, 20 : pos 20, 180 : input textInput
	objsize 100, 20 : pos 20, 210 : chkbox "斜体", gIsFontItalic : pos 130, 210 : chkbox "シャドウ", gIsFontShadow : pos 250, 210 : chkbox "アウトライン", gIsFontOutline
	objsize 60, 20 : pos 600, 400 : button gosub "終わる", *implReturnTo
	return

#deffunc global guiTestFramePush
	guiTestFrameInit; ともあれ初期化
	framePush "guiTestFrame", *guiTestFrameCompute, *guiTestFrameDraw, 0
	return

*implPushButton
	buttonPushed++
	return

*implReturnTo
	toReturn = 1
	return

*guiTestFrameCompute
	// トップ時のみ計算
	if (!frameIsTop()) { return }
	// 入力
	if (toReturn) {
		gsel 0 : cls
		framePop
	}
	// フレーム終了
	++masterFrame
	return

*guiTestFrameDraw
	// トップ時のみ描画
	if (!frameIsTop()) { return }
	// 描画内容
	gsel 0
		tMouseX = mousex : tMouseY = mousey
	gsel SID_BACK
		color 128, 128, 128 : boxf
		font "", 20
		color 255, 255, 255
		pos 20, 20
			mes `platform=${sysreq@n2(sysreq_platform@n2)} CPUCore=${sysreq@n2(sysreq_cpu_core_count@n2)} CacheLine=${sysreq@n2(sysreq_cpu_cache_line_size@n2)} RAM=${sysreq@n2(sysreq_system_ram_mb@n2)}[MB] Battery=${sysreq@n2(sysreq_battery_percentage@n2)}[%]`
			wdays = "日", "月", "火", "水", "木", "金", "土"
			mes `mouse(X=${tMouseX}, Y=${tMouseY}) time(${gettime(0)}/${gettime(1)}/${gettime(3)}(${wdays(gettime(2))}) ${gettime(4)}:${gettime(5)}:${gettime(6)}.${gettime(7)} ${strf("UTC%+d", gettime(gettime_utcoffset@n2) / 60 / 60)})`
			//ut = gettime(gettime_unixtime@n2) : lt = gettime(gettime_localtime@n2) : mes `time(lt=${lt}, ut=${ut}, ${exttime@n2(0, lt)}/${exttime@n2(1, lt)}/${exttime@n2(3, lt)}(${wdays(exttime@n2(2, lt))}) ${exttime@n2(4, lt)}:${exttime@n2(5, lt)}:${exttime@n2(6, lt)}.${exttime@n2(7, lt)})`
			mes `cwd(${dir_cur})`
			mes `exe(${dir_exe})`
		pos 240, 120 : mes buttonPushed > 0 then `ボタンが ${buttonPushed} 回押されました` else ""
		pos 240, 150 : mes `チェック状態 ${checked}`
		pos 240, 180 : mes `テキスト入力 ${textInput}`

		font "Stick", gIsFontHeight, (gIsFontItalic then 2 else 0)
		color 255, 0, 255 : objcolor 255, 255, 0
		tsx = 380 : tsy = 180
		pos tsx, tsy : mes `nidonehspのテキスト表示テスト\n入力＝${textInput}`, (gIsFontShadow then mesopt_shadow else 0) | (gIsFontOutline then mesopt_outline else 0)
		color 0, 255, 255
		pos tsx, tsy : line tsx + ginfo_mesx, tsy : line tsx + ginfo_mesx, tsy + ginfo_mesy : line tsx, tsy + ginfo_mesy : line tsx, tsy
	return

#global

